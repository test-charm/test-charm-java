def disabledPublishProjects = []
if (rootProject.ext.has('disabledPublishProjects')) {
    disabledPublishProjects = rootProject.ext.disabledPublishProjects as List
}

nexusPublishing {
    repositories {
        sonatype {
            username = System.getenv("ossrhUsername")
            password = System.getenv("ossrhPassword")
            nexusUrl.set(uri("https://ossrh-staging-api.central.sonatype.com/service/local/"))
        }
    }
}

subprojects {
    apply plugin: 'java-library'
    apply plugin: 'signing'
    apply plugin: 'maven-publish'

    java {
        toolchain {
            languageVersion = JavaLanguageVersion.of(8)
        }

        withJavadocJar()
        withSourcesJar()
    }

    publishing {
        publications {
            mavenJava(MavenPublication) {
                from(components.java)
                artifactId = project.name
                pom {
                    name = project.name
                    description = project.name
                    url = 'https://github.com/test-charm/test-charm-java/' + project.name

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'leeonky'
                            name = 'Liangchen'
                            email = 'leeonky@gmail.com'
                        }
                    }

                    scm {
                        connection = 'scm:git:https://github.com/test-charm/test-charm-java.git'
                        developerConnection = 'scm:git:git@github.com:test-charm/test-charm-java.git'
                        url = 'https://github.com/test-charm/test-charm-java.git'
                    }
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                System.getenv("SIGNING_KEY_ID"),
                System.getenv("SIGNING_KEY_IN_BASE64"),
                System.getenv("SIGNING_PASSWORD")
        )
        sign publishing.publications.mavenJava
    }

    if (disabledPublishProjects) {
        def matches = disabledPublishProjects.contains(project.path) || disabledPublishProjects.contains(project.name)
        if (matches) {
            tasks.matching { it.name.startsWith('publish') }.configureEach {
                enabled = false
            }
        }
    }
}
