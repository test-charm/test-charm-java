dependencies {
    implementation libs.prod.cucumber

    implementation project(':DAL-java')
    implementation project(':DAL-extension-basic')
    implementation project(':jfactory')
    implementation project(':jfactory-cucumber')

    testImplementation libs.test.mockserver
    testImplementation libs.test.mockserver.netty

    testImplementation libs.test.cucumber.picocontainer
    testImplementation libs.test.commons.fileupload
}

task testModuleSafe(type: Test) {
    useJUnitPlatform()
    filter {
        setExcludePatterns("RunCucumberRequiresAddOpens", "RestfulStepPatchTest")
    }
    jvmArgs = ['--illegal-access=deny']
    reports {
        html.required.set(true)
        html.outputLocation.set(file("${rootProject.buildDir}/tests-module-safe/${project.name}/html"))

        junitXml.required.set(true)
        junitXml.outputLocation.set(file("${rootProject.buildDir}/tests-module-safe/${project.name}/xml"))
    }
}

task testRequiresAddOpens(type: Test) {
    useJUnitPlatform()
    jvmArgs = ['--illegal-access=deny', '--add-opens', 'java.base/java.net=ALL-UNNAMED']
    reports {
        html.required.set(true)
        html.outputLocation.set(file("${rootProject.buildDir}/tests-requires-add-open/${project.name}/html"))

        junitXml.required.set(true)
        junitXml.outputLocation.set(file("${rootProject.buildDir}/tests-requires-add-open/${project.name}/xml"))
    }
}

task cucumberModuleSafe() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'org.testcharm.cucumber.restful', 'src/test/resources/features/module-safe']
            jvmArgs = ['--illegal-access=deny']
        }
    }
}

task cucumberRequiresAddOpens() {
    dependsOn assemble, testClasses
    doLast {
        javaexec {
            main = "io.cucumber.core.cli.Main"
            classpath = configurations.cucumberRuntime + sourceSets.main.output + sourceSets.test.output
            args = ['--plugin', 'pretty', '--glue', 'org.testcharm.cucumber.restful', 'src/test/resources/features/']
            jvmArgs = ['--illegal-access=deny', '--add-opens', 'java.base/java.net=ALL-UNNAMED']
        }
    }
}
